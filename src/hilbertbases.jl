
# All band structures can be written as ğ§ = Bğ© with páµ¢âˆˆâ„š and náµ¢âˆˆğ, and B a matrix whose 
# columns are EBRs. We can decompose B to a Smith normal form B = SÎ›T, such that all 
# allowable band structures can be written as ğ§ = Sğ³. Here, S is an integer matrix with 
# elements Sáµ¢â±¼âˆˆâ„¤. To make náµ¢ integer, we thus require záµ¢âˆˆâ„¤.

"""
    compatibility_basis([F::Smith,] brs::Union{BandRepSet, Collection{<:NewBandRep}}; kws...)
    compatibility_basis(sgnum::Integer, D::Integer=3; kwargs...)

Computes the Hilbert basis associated with a Smith normal form `F` of the EBR matrix or from
a space group number `sgnum`, which respects all compatibility relations, returning a
`SymBasis` structure. The returned basis is a non-negative integer coefficient basis
for all possible band structures {BS}.

If the method is called with `sgnum::Integer`, the underlying `BandRepSet` is also returned.

## Keyword arguments

- `algorithm::String`: controls the algorithm used by Normaliz to compute the Hilbert
basis. Choices are `"DualMode"` (default) and `"PrimalMode"`
- `spinful::Bool`: Use single- (`false`, default) or double-valued (`true`) irreps.
- `timereversal::Bool`: Assume presence (`true`, default) or absence (`false`) of
time-reversal symmetry.
- `verbose::Bool`: whether to print progress info during the Normaliz computation
(`false`, default).
"""
function compatibility_basis(F::Smith, brs::BandRepSet; 
                             algorithm::String="DualMode", verbose::Bool=false)
    # To restrict náµ¢ to only positive integers, i.e. â„•, the values of záµ¢ must be such that 
    # âˆ‘â±¼ Sáµ¢â±¼zâ±¼ â‰¥ 0. This defines a set of inequalities, which in turn defines a polyhedral
    # integer cone. This is where (Py)Normaliz comes in.
    dáµ‡Ë¢ = count(!iszero, F.SNF)           # "Dimensionality" of band structure
    S = @view F.S[:, 1:dáµ‡Ë¢]               # All the nontrivial conditions on zâ±¼

    C = PyNormaliz.Cone(inequalities = eachrow(S)) # Cone consistent w/ Sáµ¢â±¼zâ±¼ â‰¥ 0
    verbose && C.SetVerbose(true)         # Whether to print progress info
    C.Compute("HilbertBasis", algorithm)  # ComputeÂ¹ the Hilbert basis
    zsá´´_py = C.HilbertBasis()             # Python list of lists (each element a Hilbert
                                          # basis vectors in ğ³-space)
    zsá´´_vs = pyconvert(Vector{Vector{Int}}, zsá´´_py) # Convert to Julia vector of vectors
    zsá´´ = stack(zsá´´_vs)                   # Columns are now Hilbert basis vectors (ğ³-space)
    nsá´´  = S*zsá´´                          # Columns are Hilbert basis vectors in ğ§-space

    return SymBasis(nsá´´, brs, true)       # Bases of all valid symmetry vectors in ğ§-space
end

"""
    nontopological_basis([F::Smith,] brs::Union{BandRepSet, Collection{<:NewBandRep}}; kws...)
    nontopological_basis(sgnum::Integer, D::Integer=3; kwargs...)

Computes the "nontopological" Hilbert basis associated with a Smith normal form `F` of the
EBR matrix or from a space group number `sgnum`, returning a `SymBasis` structure. 
The returned basis is a non-negative integer coefficient basis for all non-topological band
structures {AI+F} (i.e. both trivial and fragile-topological).

If the method is called with `sgnum::Integer`, the underlying `BandRepSet` is also returned.

For possible keyword arguments, see `compatibility_basis(..)`.
"""
function nontopological_basis(F::Smith, brs::BandRepSet;
                              algorithm::String="DualMode", verbose::Bool=false)
    # To find the nontopological basis we build a cone subject to the inequalities 
    # (SÎ›y)áµ¢ â‰¥ 0 with yáµ¢ âˆˆ â„¤, which automatically excludes topological cases (since they
    # correspond to rational yáµ¢)
    dáµ‡Ë¢ = count(!iszero, F.SNF)  # "Dimensionality" of band structure
    S = @view F.S[:,OneTo(dáµ‡Ë¢)]  # All the nontrivial conditions on zâ±¼
    Î› = @view F.SNF[OneTo(dáµ‡Ë¢)]  # Nonzero invariant factors of Smith normal decomposition
    SÎ› = S .* Î›' # Equiv. to S*diagm(dáµ‡Ë¢, dáµ‡Ë¢, Î›); all nontrivial conditions on yáµ¢
    
    C_nontopo = PyNormaliz.Cone(inequalities = eachrow(SÎ›)) # Cone consistent w/ Sáµ¢â±¼Î›â±¼yâ±¼ â‰¥ 0
    verbose && C_nontopo.SetVerbose(true)          # Whether to print progress info
    C_nontopo.Compute("HilbertBasis", algorithm)   # ComputeÂ¹ the Hilbert basis
    ysá´´_nontopo_py = C_nontopo.HilbertBasis()      # Hilbert basis vectors in ğ²-space
    ysá´´_nontopo_vs = pyconvert(Vector{Vector{Int}}, ysá´´_nontopo_py) # Convert to Julia
    ysá´´_nontopo = stack(ysá´´_nontopo_vs)            # Hilbert basis vectors as cols (ğ²-space)

    nsá´´_nontopo = SÎ›*ysá´´_nontopo                   # Hilbert basis vectors in ğ§-space

    return SymBasis(nsá´´_nontopo, brs, false) # Bases of nontopological states in ğ§-space
end

# Convenience accessors from a space group number and dimensionality alone
for f in (:compatibility_basis, :nontopological_basis)
    @eval begin
        function $f(sgnum::Integer, D::Integer=3; 
                    algorithm::String="DualMode", verbose::Bool=false,
                    spinful::Bool=false, timereversal::Bool=true, allpaths::Bool=false)
            brs = bandreps(sgnum, D; allpaths=allpaths, spinful=spinful, timereversal=timereversal)
            B   = stack(brs) # matrix with columns of EBRs.
            F   = smith(B)   # Smith normal decomposition of B

            return $f(F, brs, algorithm=algorithm, verbose=verbose), brs
        end
        $f(brs::BandRepSet; kws...) = $f(smith(stack(brs)), brs; kws...)
        function $f(F::Smith, brs::Collection{NewBandRep{D}}; kws...) where D
            _brs = convert(BandRepSet, brs)
            return $f(F, _brs; kws...)
        end
        function $f(brs::Collection{NewBandRep{D}}; kws...) where D
            F = smith(stack(brs))
            _brs = convert(BandRepSet, brs)
            return $f(F, _brs; kws...)
        end
    end
end

"""
$(TYPEDSIGNATURES)

Compute the trivial and fragile indices of a _nontopological_ `SymBasis`, `sb_nontopo`, by 
determining whether or not each has a positive-coefficient expansion in elementary band
representations (EBRs).
The EBRs are given either through a `brs::BandRepSet` or through its matrix representation
`B = stack(brs)`.

Returns trivial indices `trivial` and fragile indices `fragile` (indexing into the basis
vectors in `sb_nontopo`) as a named tuple with the corresponding field names.
"""
function split_fragiletrivial(ns::AbstractVector{<:AbstractVector{<:Integer}},
                              B::AbstractMatrix)
    # Every (not-nontrivial) vector of `ns` that has a non-negative integer coefficient
    # expansion in EBRs, i.e. in `B`, represents a trivial basis element. All other elements
    # represent fragile basis elements. We can just test whether such a solution exists
    # through constrained optimization, and then partition into trivial and fragile 
    # categories
    # NOTE: This method explicitly assumes that it is _never_ provided any nontrivial
    #       symmetry vectors. If it is, it will misclassify them as fragile.
    # TODO: Maybe just split into all variants, including nontrivial?
    Ná´±á´®á´¿ = size(B, 2)
    trivial_idxs = Int[]; fragile_idxs = Int[]
    for (j, nÊ²) in enumerate(ns)
        m = Model(GLPK.Optimizer)
        @variable(m, c[1:Ná´±á´®á´¿] >= 0, Int)
        @constraint(m, B*c .== nÊ²)
        optimize!(m)

        # Check to see what the termination status (i.e. result) of the optimization was 
        status = termination_status(m)
        if status == MOI.OPTIMAL         # A feasible solution was found â‡’ trivial!
            push!(trivial_idxs, j)
        elseif status == MOI.INFEASIBLE  # No feasible solution exists   â‡’ fragile!
            push!(fragile_idxs, j)
        else
            error("Unexpected termination status $status")
        end
    end

    return (trivial=trivial_idxs, fragile=fragile_idxs)
end
function split_fragiletrivial(sb_nontopo::SymBasis, B::AbstractMatrix)
    if sb_nontopo.compatbasis
        throw(DomainError(sb_nontopo, "provided `SymBasis` must have `compatbasis=false`"))
    end
    return split_fragiletrivial(parent(sb_nontopo), B)
end
function split_fragiletrivial(
    sb_nontopo::SymBasis, 
    brs::Union{Collection{<:NewBandRep}, BandRepSet}
)
    return split_fragiletrivial(sb_nontopo, stack(brs))
end

# Footnotes:
# Â¹â¾ We want to control the algorithm used to calculate the Hilbert basis in Normaliz: I've
# found that Normaliz usually picks the "PrimalMode" algorithm (not always though), despite
# the documentation stating that "DualMode" usually is better for cones defined by
# inequalities. Through tests, I've found that there is usually a speedup of order 2-10,
# if the "DualMode" algorithm is picked instead; occassionally, up to Ã—100s. The speedup 
# seems to be especially large for the systems that are (very) hard to solve (e.g. 131). 
# To force "DualMode", we use the method `C.Compute(<quantity-to-compute>, <algorithm>)`, 
# which then calculates `<quantity-to-compute>` and stores it in `C` (here, "HilbertBasis");
# it can then subsequently be retrieved from `C` with `C.HilbertBasis()`.
# Some situations remain effectively out of reach still: for bosons without time-reversal
# symmetry, there are a handful of SGs for which nontopological_basis(..) does not seem to
# terminate in a reasonable amount of time (15h+ tested).